plugins {
    id "org.sonarqube" version "4.0.0.2929"
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
// apply plugin: 'kotlin-kapt'
apply plugin: "kotlin-parcelize"

apply plugin: 'com.google.gms.google-services'
// Apply the App Distribution Gradle plugin
apply plugin: 'com.google.firebase.appdistribution'
// Add the Firebase Crashlytics plugin.
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'

android {

    compileSdkVersion 34
    buildToolsVersion '30.0.3'

    buildFeatures {
        dataBinding true
        viewBinding true
    }    

    dataBinding {
        //noinspection DataBindingWithoutKapt
        enabled = true
    }
    viewBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "nl.fdmg.fd"
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 100
        versionName "2.31.0-SNAPSHOT"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        resConfigs "en", "nl" // Only package these languages from libraries
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    flavorDimensions "stage"

    productFlavors {
        dev {
            dimension "stage"
            versionNameSuffix = ".DEV"
            applicationIdSuffix = ".dev"
            resValue 'string', 'APP_NAME', '"FD DEV"'
            resValue 'string', 'AUTH_SCHEMA', 'fd-dev'
            resValue 'string', 'CONTENT_AUTHORITY', 'nl.fd.dev'
            resValue "string", "HOSTNAME", "\"dev.fd.nl\""
            buildConfigField "String", "CONTENT_AUTHORITY", "\"nl.fd.dev\""
            buildConfigField "String", "ENVIRONMENT", "\"development\""
            buildConfigField "String", "DOMAIN_URL", "\"fd.nl\""
            buildConfigField "String", "BASE_URL", "\"https://dev.fd.nl/\""

            buildConfigField "String", "FD_APP_SERVICE_BASE_URL", getProps("FD_APP_SERVICE_BASE_URL")

            buildConfigField "String", "SSO_LOGIN_BASE_URL", "\"https://dev.fd.nl/login?kc_idp_hint=\""
            buildConfigField "String", "STOCK_MARKET_URL", "\"https://beurs.acc.fd.nl/app\""
            buildConfigField "boolean", "AUTH_REQUIRED", "true"
            buildConfigField "boolean", "FORCE_WEBVIEW", "false"
            buildConfigField "int", "REFRESH_FREQUENCY_IN_SECONDS", "20"

            buildConfigField "String", "CHARTBEAT_DOMAIN", "\"test.fd.nl\""
        }


    }

    buildTypes {
        debug {
            minifyEnabled false
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-debug.pro'
            buildConfigField "String", "BUILD_NUMBER", "\"\""
            buildConfigField "String", "GOOGLE_API_KEY", "\"DvYhp6I1WDuaxY1Sdtg9HNwCmghwk2Yf8\""
            buildConfigField "String[]", "TEST_DEVICE_IDS", "{}"
            buildConfigField "boolean", "SHOW_DROPCAP_OUTLINES", "false"    // Outlines around dropcap to show font baseline, margin etc
            buildConfigField "boolean", "SHOW_CONTENT_DEBUG_INFO", "false"   // Extra visual bar with article id, ad unit in the content
            buildConfigField "boolean", "SHOW_POPUP_DEBUG_INFO", "false"   // Little pop-ups with event-based messages
        }

        release {
            minifyEnabled true
            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BUILD_NUMBER", "\"${System.getenv("BUILDOZER_BUILDNUMBER")}\""
            buildConfigField "boolean", "SHOW_DROPCAP_OUTLINES", "false"
            buildConfigField "boolean", "SHOW_CONTENT_DEBUG_INFO", "false"
            buildConfigField "boolean", "SHOW_POPUP_DEBUG_INFO", "false"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget=11
        freeCompilerArgs += [
                "-Xjvm-default=enable",
        ]
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions {
        abortOnError false
        ignore("UseCompatLoadingForDrawables")
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.10.1'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    // older versions of appcompat cause crash when loading WebView in SDK 21 devices, see https://stackoverflow.com/questions/41025200/android-view-inflateexception-error-inflating-class-android-webkit-webview
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.annotation:annotation:1.6.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'androidx.browser:browser:1.5.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "com.google.android.material:material:1.9.0" // required for tabLayout
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.window:window:1.1.0'

    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    //noinspection AnnotationProcessorOnCompilePath
    implementation 'org.projectlombok:lombok:1.18.22'
    implementation 'androidx.constraintlayout:constraintlayout-solver:2.0.4'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    //noinspection AnnotationProcessorOnCompilePath
    testImplementation 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.12'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation "com.google.dagger:dagger:2.48.1"
    annotationProcessor "com.google.dagger:dagger-compiler:2.48.1"

    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation('com.github.bright:slf4android:0.1.6@aar')

    // Twitter
    implementation 'com.twitter.sdk.android:twitter-core:3.3.0@aar'
    implementation 'com.twitter.sdk.android:tweet-ui:3.3.0@aar'
    //noinspection GradleDependency - version pinned to what tweet-ui expects
    implementation 'com.squareup.picasso:picasso:2.5.2'


    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"

    implementation 'com.jakewharton:disklrucache:2.0.2'

    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.parceler:parceler-api:1.1.13'
    annotationProcessor 'org.parceler:parceler:1.1.13'


    // Add the Firebase Crashlytics SDK.
    releaseImplementation('com.google.firebase:firebase-crashlytics:18.4.0') {
        transitive = true
    }

    // To be able to test crashlytics on debug branches
    implementation 'com.google.firebase:firebase-crashlytics'

    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:30.3.1')

    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-perf'
    implementation 'com.google.firebase:firebase-invites:17.0.0'

    // Recommended: Add the Google Analytics SDK.
    implementation 'com.google.firebase:firebase-analytics'

    implementation "com.google.android.gms:play-services-tagmanager:18.0.3"
    implementation "com.google.android.gms:play-services-ads:22.2.0"

    // comScore Analytics
    implementation 'com.comscore:android-analytics:6.9.0'

    // Chartbeat analytics
    implementation 'com.github.chartbeat:android_sdk:v1.6.7'

    // Facebook
    implementation('com.facebook.android:facebook-android-sdk:4.28.0') {
        exclude group: "com.android.support"
    }
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'joda-time:joda-time:2.10.1'

    // Image loading
    implementation("com.github.bumptech.glide:glide:4.12.0") {
        exclude group: "com.android.support"
    }
    annotationProcessor "com.github.bumptech.glide:compiler:4.12.0"

    implementation 'com.squareup.okhttp3:logging-interceptor:4.6.0'
    implementation("com.github.bumptech.glide:okhttp3-integration:4.11.0@aar") {
        transitive = false // Use our own OkHTTP version
    }
    implementation("com.github.bumptech.glide:recyclerview-integration:4.11.0") {
        exclude group: "com.android.support"
        transitive = false
    }
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'

    implementation 'com.github.tommus:youtube-android-player-api:1.2.3'

    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    implementation 'com.novoda:drop-cap:1.1.0'

    // Link handling
    implementation 'me.saket:better-link-movement-method:2'

    // Ask for Play store rating / review
    //implementation 'com.github.iamrobj:RatingManager:0.3'
    implementation(name:'jetified-RatingManager-0.3', ext:'aar')

    // Sourcepoint
    implementation 'com.sourcepoint.cmplibrary:cmplibrary:6.6.1'

    implementation 'com.github.barteksc:android-pdf-viewer:2.8.2'  //epaper pdf handling
    implementation 'com.tom-roush:pdfbox-android:2.0.4.0'

    implementation "androidx.arch.core:core-testing:2.2.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android"

    // JWT Tokens
    api 'io.jsonwebtoken:jjwt-api:0.11.2'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2'
    runtimeOnly('io.jsonwebtoken:jjwt-orgjson:0.11.2') {
        exclude group: 'org.json', module: 'json' //provided by Android natively
    }

    // Unit Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.robolectric:robolectric:4.5.1"
    testImplementation "org.robolectric:multidex:3.4.2"
    testImplementation "org.mockito:mockito-core:3.7.0"

    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.3"
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'

}
repositories {
    mavenCentral()
}

def getProps(String propName) {
    def propsFile = rootProject.file('local.properties')
    def props = new Properties()
    if (propsFile.exists()) {
        props.load(new FileInputStream(propsFile))
    } else {
        props.setProperty("FD_APP_SERVICE_BASE_URL", "\"https://dev-api.fdmg.org/private/fd/app/\"")
    }
    return props[propName]
}
